// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Put your code here:
    // 000000000000000_2 = 0_10 = first RAM16K address
    // 011111111111111_2 = 16383_10 = last RAM16K address
    // 100000000000000_2 = 16384_10 = first pixel address
    // 101111111111111_2 = 24575_10 = last pixel address
    // 110000000000000_2 = 24576_10 = key press ASCII value
    DMux4Way(in=load, sel=address[13..14], a=ram0, b=ram1, c=scnld, d=ignore);
    Or(a=ram0, b=ram1, out=ramld);
    RAM16K(in=in, load=ramld, address=address[0..13], out=ramout);
    Screen(in=in, load=scnld, address=address[0..12], out=scnout);
    Keyboard(out=kbdout);
    Mux4Way16(a=ramout, b=ramout, c=scnout, d=kbdout, sel=address[13..14], out=out);
}