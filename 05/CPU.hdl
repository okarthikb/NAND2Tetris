// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    
    // instruction format - 111 a cccccc ddd jjj
    // a cccccc - computation spec
    // ddd - destination spec
    // jjj - jump spec
    
    // @xyz
    // M = ... (manipulates RAM[xyz])
    // A = ... (manipulates A register value)
    // D = ... (manipulates D register value)
    // @abc
    // D/M/A;JMP/JEQ/JNE/JGT/JGE/JLT/JLE/null (jump to instruction)
    Not(in=instruction[15], out=sel);
    And(a=instruction[15], b=instruction[4], out=writeD);
    Or(a=sel, b=instruction[5], out=writeA);
    Mux16(a=out, b=instruction, sel=sel, out=inA);
    ARegister(in=inA, load=writeA, out=A, out[0..14]=addressM);
    And(a=instruction[15], b=instruction[12],out=AorM);
    Mux16(a=A, b=inM, sel=AorM, out=arg);
    ALU(x=D, y=arg, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=out, out[0..7]=outf, out[8..15]=outs, zr=zr, ng=ng);
    DRegister(in=out, load=writeD, out=D);
    // null, JGT, JEQ, JGE, JLT, JNE, JLE, JMP
    And(a=instruction[15], b=instruction[3], out=writeM);
    Not(in=zr, out=nzr);
    Not(in=ng, out=nng);
    And(a=instruction[15], b=instruction[0], out=JGT);
    And(a=nng, b=nzr, out=nngnzr);
    And(a=JGT, b=nngnzr, out=compa);
    And(a=instruction[15], b=instruction[1], out=JEQ);
    And(a=JEQ, b=zr, out=compb);
    And(a=instruction[15], b=instruction[2], out=JLT);
    And(a=JLT, b=ng, out=compc);
    Or(a=compa, b=compb, out=comp);
    Or(a=compc, b=comp, out=load);
    PC(in=A, load=load, inc=true, reset=reset, out[0..14]=pc);
}
